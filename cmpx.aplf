 cmpx←{                                          ⍝ Approx expression timings.
     ⍺←⍬                                         ⍝ options: raw cpu cols.
     1=≡,⍵:⍺ ∇⊂,⍵                                ⍝ single expression: enclose.
     ⍺{                                          ⍝ options.
         (⍎⍵)-⎕AI                                ⍝ time of ⍺ expr-iterations.
     }{⎕IO ⎕ML←0                                 ⍝ local sysvars (see Notes).
         dflt←{⍵+⍺×⍵=0}                          ⍝ ⍺ default if ⍵=0.
         raw cpu cols←0 0 30 dflt¨3↑⍺            ⍝ raw/cpu times, hist width.
         clock←(2-cpu)∘⊃                         ⍝ 0:elapsed, 1:cpu
         count nett←1 ⍺⍺{                        ⍝ loop count and times.
             times←⍺ ⍺⍺{                         ⍝ time for each expression.
                 head←'{0<⍵:∇⍵-1{⍺}'             ⍝ loop pre- and
                 tail←'⋄⎕AI}⍺'                   ⍝ post-amble.
                 clock ⍺ ⍺⍺ head,⍵,tail          ⍝ time for ⍺ loops of expr.
             }¨⍵                                 ⍝ ... for each expression.
             1000>+/times÷⍴,⍵:(⍺×2)∇ ⍵           ⍝ too quick: reiterate × 2.
             null←'{0<⍵:∇⍵-{⍵}1⋄⎕AI}⍺'           ⍝ null loop for "control" time.
             ⍺,⊂times-clock ⍺ ⍺⍺ null            ⍝ times - null loop overhead.
         }⍵                                      ⍝ vector of expressions.
         hist←↑{(⌊0.5+cols×⍵÷⌈/⍵)⍴¨'⎕'}nett      ⍝ simple histogram.
         raw:nett÷count×1000                     ⍝ raw numeric result.
         secs←↑7 ¯3∘⍕¨nett÷count×1000            ⍝ e-format seconds.
         rels←⌊100×¯1+{⍵÷⊃⍵}1⌈nett               ⍝ percentages relative to 1st.
         sign←{(1+×⍵)⊃'- +'}                     ⍝ sign: - +.
         fpcs←⌽↑{'%',⌽(sign ⍵),⍕|⍵}¨rels         ⍝ formatted percentages.
         diff←{⍵⊃' *'}¨×{⍵⍳⍵},⍵⍵¨⍵               ⍝ result differences.
         exps←diff,' ',↑⍵                        ⍝ exprs with diff markers.
         pref←{↑⍺∘,¨↓⍵}                          ⍝ ⍺-prefix of rows of matrix ⍵.
         cmps←' | 'pref fpcs,' ',hist            ⍝ percents and histogram.
         1=⍴,⍵:secs                              ⍝ single expr: time only.
         exps,' → 'pref secs,cmps                ⍝ [*] expr → secs | %s histo.
     }{                                          ⍝ right operand:
         ⍎⍵                                      ⍝ expr executed in native envt.
     }⍵                                          ⍝ vector of subject exprs.
 }
