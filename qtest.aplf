 qtest←{
     assert ¯1=×Q qi ¯12345:
     assert 0=×Q qi 0:
     assert 1=×Q qi 59.3:

     assert(⌊Q qi 56.4)≡qi 56:
     assert(⌊Q qi ¯56.4)≡qi ¯57:
     assert(⌊Q qi 12345)≡qi 12345:
     assert(⌊Q qi ¯12345)≡qi ¯12345:
     assert(⌊Q qi 0)≡qi 0:

     assert(⌈Q qi 56.4)≡qi 57:
     assert(⌈Q qi ¯56.4)≡qi ¯56:
     assert(⌈Q qi 12345)≡qi 12345:
     assert(⌈Q qi ¯12345)≡qi ¯12345:
     assert(⌈Q qi 0)≡qi 0:

     assert(|Q qi 4.5)≡qi 4.5:
     assert(|Q qi ¯4.5)≡qi 4.5:
     assert(|Q qi 0)≡qi 0:

     assert((qi¨x)|Q¨qi¨y)≡qi¨(x←2/1.3 ¯1.3)|y←4⍴2.8 ¯2.8:
     assert((⊂qi 0)|Q¨qi¨x)≡qi¨x←¯4.5 0 12.4:

     assert(!Q qi 0)≡qi 1:
     assert(!Q qi 1)≡qi 1:
     assert(!Q qi 12)≡qi!12:
     assert(⍕Q!Q qi 39)≡'20397882081197443358640281739902897356800000000':

     assert((qi 10)!Q qi 19)≡qi 10!19:
     x←'1647278652451762678788128833110870712983038446517480945400'
     assert(⍕Q(qi 120)!Q qi 200)≡x:

     assert((⊂qi 4÷3)*Q¨qi¨⍳10)≡qi¨(4÷3)*⍳10:
     assert((⊂qi 4÷3)*Q¨qi¨-⍳10)≡qi¨(3÷4)*⍳10:
     assert((qi 0)*Q qi 5)≡qi 0:
     assert((⊂qi ¯1)*Q¨qi¨x)≡qi¨¯1*x←?4 5⍴100:
     assert((⊂qi 1)*Q¨qi¨x)≡qi¨(⍴x)⍴1:

     x←qi¨y←¯3.47 ¯2 0 2 3.47
     assert(∘.(<Q)⍨x)≡(∘.<⍨y):
     assert(∘.(≤Q)⍨x)≡(∘.≤⍨y):
     assert(∘.(=Q)⍨x)≡(∘.=⍨y):
     assert(∘.(≠Q)⍨x)≡(∘.≠⍨y):
     assert(∘.(≥Q)⍨x)≡(∘.≥⍨y):
     assert(∘.(>Q)⍨x)≡(∘.>⍨y):

     x←(+Q).(×Q)
     assert(⍕∘.=⍨⍳5)≡⍕Q(⌹Q a)x⊢a←qi¨?5 5⍴50:
     assert(b⌹Q a)≡(⌹Q a)x⊢b←qi¨?5⍴50:
     assert(a x b⌹Q a)≡b:
     assert(⌹Q a)≡(⌹Q(⍉a)x a)x⍉a←qi¨?10 5⍴50:
     assert(b⌹Q a)≡(⌹Q(⍉a)x a)x(⍉a)x⊢b←qi¨?10⍴50:

     assert qtestfmt ⍬:

     assert'assertion failure'≡(qi 1 2)÷Q Err qi 0:
     assert'assertion failure'≡!Q Err qi ¯1:
     assert'assertion failure'≡!Q Err qi 1.2:

     1
 }
